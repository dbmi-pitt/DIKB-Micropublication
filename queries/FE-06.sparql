# SPARQL version: 1.1
# DIKB MP/NP version: 2015-10-09

PREFIX aoOld: <http://purl.org/ao/core/> # needed for AnnotationSet and item until the equivalent is in Open Data Annotation
PREFIX cnt: <http://www.w3.org/2011/content#>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX dikbD2R: <http://dbmi-icode-01.dbmi.pitt.edu/dikb/vocab/resource/>
PREFIX dikbEvidence: <http://dbmi-icode-01.dbmi.pitt.edu/dikb-evidence/DIKB_evidence_ontology_v1.3.owl#>
PREFIX gcds: <http://www.genomic-cds.org/ont/genomic-cds.owl#>
PREFIX mp: <http://purl.org/mp/> # namespace for micropublication
PREFIX ncbit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX pav: <http://purl.org/pav>
PREFIX poc: <http://dbmi-icode-01.dbmi.pitt.edu/mp/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX siocns: <http://rdfs.org/sioc/ns#>
PREFIX swande: <http://purl.org/swan/1.2/discourse-elements#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

# Which single evidence items are unique support for multiple claims (i.e. it is the only piece of evidence)?

SELECT DISTINCT xsd:string(?source) as ?evidence_source
FROM <inferred-graph-oa-mp.xml>
WHERE 
{
      ?aURN oa:hasSource ?source.  
      
      ?anItem oa:hasTarget ?aURN.
      
      ?anItem oa:hasBody ?data.
            
      ?data mp:supports ?claim.
      
      ?claim a mp:Claim.
      
  {      
      SELECT DISTINCT ?claim count(DISTINCT ?source)
      FROM <inferred-graph-oa-mp.xml>
      WHERE 
      {
	   ?claim a mp:Claim.
	   
	   ?data mp:supports ?claim.
	   
	   ?anItem oa:hasBody ?data.
	   
	   ?anItem oa:hasTarget ?aURN.
	   
	   ?aURN oa:hasSource ?source.
      }
      GROUP BY ?claim
      HAVING ( count(DISTINCT ?source) = 1 )
  }
 
}
GROUP BY ?source
HAVING ( count(DISTINCT ?claim) > 1 )
